buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'de.undercouch:gradle-download-task:3.1.1'
    }
}

import de.undercouch.gradle.tasks.download.Download

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'de.undercouch.download'

ext.thriftBinary = { arch ->
    project.property("thrift.release.url") + '/thrift-' + arch
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'junit:junit:4.12'
}

task downloadThriftBinaries(type: Download) {
    src(['osx-x86_64', 'linux-x86_64'].collect { rootProject.ext.thriftBinary(it) })
    dest "$buildDir/main/resources/"
    onlyIfNewer true
}
tasks.processResources.dependsOn downloadThriftBinaries
